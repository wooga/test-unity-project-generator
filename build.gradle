plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'nebula.release' version '6.3.5'
}

group "net.wooga.test"

dependencies {
    implementation 'net.lingala.zip4j:zip4j:1.3.2'
    implementation 'junit:junit:4.12'
    implementation 'commons-io:commons-io:2.6'

    testImplementation 'org.codehaus.groovy:groovy-all:2.4.15'
    testImplementation 'org.spockframework:spock-core:1.1-groovy-2.4'
    testImplementation 'junit:junit:4.12'
}

repositories {
    jcenter()
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publish = true
    publications = ['Bintray']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'wooga'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/wooga/test-unity-project-generator.git'
        labels = ['test', 'JUnit', 'unity']
        version {
            name = project.version.toString()
            vcsTag = project.version.toString()
            released = new Date()
        }
    }
}

publishing {
    publications {
        Bintray(MavenPublication) {
            from components.java
        }
    }
}

[project.tasks.finalSetup, project.tasks.candidateSetup, project.tasks.snapshotSetup].each { it.dependsOn(build)}
